group 'com.github'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

sourceSets {
    itest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/itest/java')
        }
        resources.srcDir file('src/itest/resources')
    }
}
repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.1'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
}

task itest(type: Test) {
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath
    outputs.upToDateWhen { false }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

// Jetty configuration
jettyRun {
    daemon = true
    httpPort = 9000
    contextPath = '/'
    classpath = configurations.compile
    stopPort = 9100
    stopKey = 'stop'
}

jettyStop {
    stopPort = 9100
    stopKey = 'stop'
}

check.dependsOn itest
itest.dependsOn jettyRun
jettyStop.mustRunAfter itest
